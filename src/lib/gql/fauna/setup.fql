// * Schema must be uploaded before running setup script!
// * Please run blocks one at a time to prevent race conditions, or use `Do` to execute them in order.

/* VSCode Fauna (+extension) tips
Easily format for Do: wrap all in Do(), then select a closing parens + whitespace and ctrl+shift+l to get all cursors. 
Finally add the comma.
Must have ext and .faunarc w/ key for below:
Running queries from Fauna FQL files: Regex replace comments: //.+ (Create Query -> Copy/Paste -> Replace -> Run 
Query/CTRL+Enter)
*/

/* UDF guidelines:
- GraphQL query Lambda resolvers must ALWAYS accept an array, even if only a single arg. This creates distinction 
  between GraphQL Lambdas and normal Lambdas. This makes sense GraphQL query variables are declared in an ordered (array) style. 
*/

// Auth

// Login with only email, for User collection/type
Update(Function('login_user'), {
  body: Query(
    Lambda(
      ['input'],
      // Auth and user info payload
      Let(
        {
          // Let() declarations evaluate variable expressions in order, like Do()
          userMatch: Match(Index('unique_User_email'), Select('email', Var('input'))),
          // Create user if doesn't exist
          userRef: If(
            IsNonEmpty(Var('userMatch')),
            Select('ref', Get(Var('userMatch'))),
            Select(
              'ref',
              Create(Collection('User'), {
                // ⚙ User creation defaults
                data: Merge(Var('input'), { onboard: true, created: Now() }),
              })
            )
          ),
          tokenDoc: Create(Tokens(), {
            instance: Var('userRef'),
            // ⚙ Access token expiry
            // ? Need a shorter active token expiry? Typically secure apps go 10m/20m/1hr, if user data security
            ttl: TimeAdd(Now(), 30, 'days'), //? doesn't matter in your app, you can increase the access token duration.
          }),
        },
        {
          userInfo: Var('userRef'), // Fauna GraphQL API codegens Get() using doc ref of the type/collection
          exp: Select('ttl', Var('tokenDoc')),
          token: Select('secret', Var('tokenDoc')),
        }
      )
    )
  ),
})

Update(Function('logout_user'), {
  body: Query(Lambda(['input'], Logout(Select('allDevices', Var('input'))))),
})

// Internal index that returns who the user is following rather than the Follow doc by auto-generated FQL via SDL
// This is almost always the desired behavior when searching for all the people the user follows.
CreateIndex({
  name: '_following_by_user',
  unique: false,
  serialized: true,
  source: Collection('Follow'),
  terms: [
    {
      field: ['data', 'user'],
    },
  ],
  values: [
    {
      field: ['data', 'follows'],
    },
  ],
})

// TODO: Paginate may return null results in which case you'd need to Get() the actual data, see get_profile_photos for
// TODO: the logic that does this. Also, test if sending back an obj w/ the ref on it instead of using Get() works.
// ? If reusable code desired, can abstract this UDF into a more general UDF that takes the size, after, before, & `match` as a variable and invoke it.
Update(Function('get_timeline'), {
  body: Query(
    Lambda(
      // yep, with GraphQL paginate: true on resolver, all values defined in schema will be first and any of the paginate
      // variables are after.
      ['username', 'size', 'after', 'before'],
      Let(
        {
          match: If(
            Equals(Var('username'), null),
            // Timeline
            Reverse(
              Join(
                // Potential optimization incurs significantly more calls and will not be applied here as it handles an edge case.
                // Paginate could be used on these matches to prevent resulting Join set from being too large (100k) if following too many users w/ too many photos.
                Match(Index('_following_by_user'), CurrentIdentity()),
                Index('photo_author_by_user')
              )
            ),
            If(
              Equals(Var('username'), ''),
              // Explore
              Reverse(Documents(Collection('Photo'))),
              // Profile Photos
              Reverse(Join(Match(Index('unique_User_username'), Var('username')), Index('user_photos_by_user')))
            )
          ),
        },
        Map(
          If(
            Equals(Var('before'), null),
            If(
              Equals(Var('after'), null),
              Paginate(Var('match'), { size: Var('size') }),
              Paginate(Var('match'), { size: Var('size'), after: Var('after') })
            ),
            Paginate(Var('match'), { size: Var('size'), before: Var('before') })
          ),
          Lambda(
            'docRef',
            Let(
              { photoDoc: Get(Var('docRef')) },
              Merge(Var('photoDoc'), {
                data: Merge(Select('data', Var('photoDoc')), {
                  likedByUser: Exists(
                    Match(Index('photo_liked_by_photo_and_user'), [Var('docRef'), CurrentIdentity()])
                  ),
                }),
              })
            )
          )
        )
      )
    )
  ),
})

Update(Function('like_photo'), {
  body: Query(
    Lambda(
      ['input'],
      Let(
        {
          photoRef: Ref(Collection('Photo'), Select('photoID', Var('input'))),
          likedMatch: Match(Index('photo_liked_by_photo_and_user'), [Var('photoRef'), CurrentIdentity()]),
        },
        Do(
          // Create likes
          If(
            Select('value', Var('input')),
            // like
            If(
              Exists(Var('likedMatch')),
              null,
              Create(Collection('photo_liked'), {
                data: {
                  userID: CurrentIdentity(),
                  photoID: Var('photoRef'),
                },
              })
            ),
            // unlike
            If(Exists(Var('likedMatch')), Delete(Select('ref', Get(Var('likedMatch')))), null)
          ),
          Let(
            {
              // Update likeCount on photo
              photoDoc: Update(Var('photoRef'), {
                data: { likeCount: Count(Match(Index('photo_liked_by_photo'), Var('photoRef'))) },
              }),
            },
            // return likedByUser field
            Merge(Var('photoDoc'), {
              data: Merge(Select('data', Var('photoDoc')), {
                likedByUser: Exists(
                  Match(Index('photo_liked_by_photo_and_user'), [Var('photoRef'), CurrentIdentity()])
                ),
              }),
            })
          )
        )
      )
    )
  ),
})

Update(Function('get_photo'), {
  body: Query(
    Lambda(
      ['input'],
      Let(
        {
          photoRef: Ref(Collection('Photo'), Select('photoID', Var('input'))),
          photoDoc: Get(Var('photoRef')),
        },
        Merge(Var('photoDoc'), {
          data: Merge(Select('data', Var('photoDoc')), {
            likedByUser: Exists(Match(Index('photo_liked_by_photo_and_user'), [Var('photoRef'), CurrentIdentity()])),
          }),
        })
      )
    )
  ),
})

Update(Function('get_user_by_username'), {
  body: Query(
    Lambda(
      ['username'],
      Let(
        { userDoc: Get(Match(Index('unique_User_username'), Var('username'))) },
        Merge(Var('userDoc'), {
          data: Merge(Select('data', Var('userDoc')), {
            followedByUser: Exists(
              Intersection(
                Match(Index('following_by_user'), CurrentIdentity()),
                Match(Index('followers_by_user'), Select('ref', Var('userDoc')))
              )
            ),
          }),
        })
      )
    )
  ),
})

Update(Function('follow_user'), {
  body: Query(
    Lambda(
      ['input'],
      Let(
        {
          userRef: Ref(Collection('User'), Select('userID', Var('input'))),
          // all the users the user is following
          followingMatch: Match(Index('following_by_user'), CurrentIdentity()),
          // all users following who the user is trying to follow
          followersMatch: Match(Index('followers_by_user'), Var('userRef')),
          // check if the user is already following, however we're not creating a new index with multiple match terms
          followMatch: Intersection(Var('followingMatch'), Var('followersMatch')),
        },
        Do(
          // Create Follow doc
          If(
            Select('value', Var('input')),
            // follow
            If(
              Exists(Var('followMatch')),
              null,
              Create(Collection('Follow'), {
                data: {
                  user: CurrentIdentity(),
                  follows: Var('userRef'),
                },
              })
            ),
            // unfollow
            If(Exists(Var('followMatch')), Delete(Select('ref', Get(Var('followMatch')))), null)
          ),
          // Update followerCount and followingCount on both user and who they're following, and refresh w/ new match + count
          Update(CurrentIdentity(), {
            data: { followingCount: Count(Match(Index('following_by_user'), CurrentIdentity())) },
          }),
          // Return who to un/follow doc
          Let(
            {
              userDoc: Update(Var('userRef'), {
                data: { followerCount: Count(Match(Index('followers_by_user'), Var('userRef'))) },
              }),
            },
            Merge(Var('userDoc'), {
              data: Merge(Select('data', Var('userDoc')), {
                followedByUser: Select('value', Var('input')),
              }),
            })
          )
        )
      )
    )
  ),
})

// Search for usernames
// Exact contains using Ngrams
CreateIndex({
  name: 'User_search_by_username',
  // we actually want to sort to get the shortest word that matches first
  source: [
    {
      // If your collections have the same property that you want to access you can pass a list to the collection
      collection: [Collection('User')],
      fields: {
        searchTerms: Query(
          Lambda(
            'User',
            Distinct(
              Union(
                Let(
                  {
                    // TODO: Reduce this array if you want less ngrams per word.
                    indexes: [0, 1, 2, 3, 4, 5, 6, 7, 8, 9],
                    indexesFiltered: Filter(
                      Var('indexes'),
                      // filter out the ones below 0
                      Lambda('l', GT(Var('l'), 0))
                    ),
                    ngramsArray: Map(
                      Var('indexesFiltered'),
                      // you can also cleanup the input data by surrounding the Lowercase in a replace
                      Lambda('l', NGram(LowerCase(Select(['data', 'username'], Var('User'))), Var('l'), Var('l')))
                    ),
                  },
                  Var('ngramsArray')
                )
              )
            )
          )
        ),
      },
    },
  ],
  terms: [{ binding: 'searchTerms' }],
})

Update(Function('search_user'), {
  body: Query(
    Lambda(
      ['username', 'size', 'after', 'before'],
      Let(
        { match: Match(Index('User_search_by_username'), Var('username')) },
        Map(
          If(
            Equals(Var('before'), null),
            If(
              Equals(Var('after'), null),
              Paginate(Var('match'), { size: Var('size') }),
              Paginate(Var('match'), { size: Var('size'), after: Var('after') })
            ),
            Paginate(Var('match'), { size: Var('size'), before: Var('before') })
          ),
          Lambda('docRef', Get(Var('docRef')))
        )
      )
    )
  ),
})
